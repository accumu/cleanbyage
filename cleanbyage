#!/usr/bin/perl
# vim:ts=4:sw=4:et:

# cleanbyage - Remove oldest files from a directory tree based on
#              atime. Typically used for shared tmp/scratch and cache
#              file systems. Supports separate start/stop space/inode
#              limits to enable cleaning out more files when triggered.
#              Can also print age statistics, handy to evaluate filesystem
#              size and cleanup settings.

#  Copyright 2006 Tomas Forsman (né Ögren)
#  Copyright 2008 Niklas Edmundsson
#
#  Licensed under the Apache License, Version 2.0 (the "License");
#  you may not use this file except in compliance with the License.
#  You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
#  Unless required by applicable law or agreed to in writing, software
#  distributed under the License is distributed on an "AS IS" BASIS,
#  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#  See the License for the specific language governing permissions and
#  limitations under the License.


use Sys::Hostname;
use File::Find ();
use Getopt::Std;
use Fcntl ':mode';
# Filesys::Statvfs is recommended, but df is used as a fallback.

use strict;
use warnings;

# Set the variable $File::Find::dont_use_nlink if you're using AFS,
# since AFS cheats.

# for the convenience of &wanted calls, including -eval statements:
use vars qw/*name *dir *prune/;
*name   = *File::Find::name;
*dir    = *File::Find::dir;
*prune  = *File::Find::prune;

sub wanted;
sub debug;
sub printsize;

my(%opts);

# Min free percentage
my $defminfree = 20;
my $minfree = $defminfree;
my $mincleanfree = $defminfree;
my $minifree = $defminfree;
my $defexclude = "lost\+found/.*";
my $exclude = $defexclude;

getopts('hsdm:M:i:e:D', \%opts);
if(defined($opts{m})) {
    $minfree = $opts{m};
}
if(defined($opts{M})) {
    $mincleanfree = $opts{M};
}
if(defined($opts{i})) {
    $minifree = $opts{i};
}

if($minfree < 0 || $minfree > 100) {
    die "Bad value for -m: $minfree\n";
}
if($mincleanfree < 0 || $mincleanfree > 100) {
    die "Bad value for -M: $mincleanfree\n";
}
if($mincleanfree < $minfree) {
    die "-M can't be smaller than -m (now $mincleanfree/$minfree)\n"
}
if($minifree < 0 || $minifree > 100) {
    die "Bad value for -i: $minifree\n";
}

my $filesystem=shift @ARGV;

if(defined($opts{'h'}) || !defined($filesystem)) {
    print <<EOH;
   usage: $0 [flags] directory
        -h      - This help
        -s      - Statistics mode, show fs usage stats, don't delete anything
        -d      - Don't delete directories
        -m NN   - Min free space to start cleaning (percent) (default $defminfree)
        -M NN   - Free space to stop cleaning (percent) (default $defminfree)
        -i NN   - Min free inodes (percent) (default $defminfree)
        -e RE   - Exclude RE (default $defexclude)
        -D      - Debug output
EOH
    exit 1;
}

$filesystem =~ s|/+$||;

if($filesystem eq "" || ! -d $filesystem) {
    die "Not a directory: $filesystem";
}

if($opts{'e'}) {
    $exclude = $opts{'e'};
    eval {
        "kaka" =~ m|$filesystem/$exclude|;
    };
    if($@) {
        die "Bad value for -e:\n",$@,"\n";
    }
}

my ( $fssize, $fsfree, $files, $favail ) = fsinfo($filesystem);
my $minfinode=$files*$minifree/100;
my $minfbytes=$fssize*$minfree/100;

debug "$filesystem:\nBytes\t\tInodes\n$fsfree\t$favail";

exit 0 if $fsfree > $minfbytes && $favail > $minfinode && !$opts{'s'};

$minfbytes=$fssize*$mincleanfree/100;

my %files;
my $totsize = 0; # Total size, by adding all filesizes in wanted().

File::Find::find({wanted => \&wanted}, $filesystem);

if($opts{'s'}) {
    print "Usage statistics: $filesystem\n";
    print "Total size of files found: ",printsize($totsize),"\n";
    my $increment = $totsize / 10;
    if($increment < 1) {
        print "Total size too small to do anything useful\n";
        exit 0;
    }

    printf "%-21s %-25s %s\n", "At size (% of total)", "Newest file", "Oldest file";

    my $nextinc = $increment;
    my $accumulated = 0;
    my ($newatime, $lastatime);

    foreach my $fil (sort { $files{$b}{atime} <=> $files{$a}{atime} } 
        keys %files) 
    {
        $accumulated += $files{$fil}{size};
        if(!$newatime) {
            $newatime = $files{$fil}{atime};
        }

        if($accumulated > $nextinc) {
            printf "%-21s %-25s %s\n", 
                  printsize($accumulated)." (".int(($accumulated/$totsize)*100)."%)",
                  scalar localtime($newatime),
                  scalar localtime($files{$fil}{atime});
            while($nextinc < $accumulated) {
                $nextinc += $increment;
            }
            $newatime = undef;
            $lastatime = undef;
        }
        else {
            $lastatime = $files{$fil}{atime};
        }
    }
    printf "%-21s %-25s %s\n", 
          printsize($accumulated)." (100%)", "-",
          scalar localtime($lastatime) if($lastatime);

    exit 0;
}

my @rootfiles;

foreach my $fil (sort { $files{$a}{atime} <=> $files{$b}{atime} } keys %files) {
    debug "fil: $fil  atime: $files{$fil}{atime}  size: $files{$fil}{size}";
    last if $fsfree > $minfbytes && $favail > $minfinode;

    if( $files{$fil}{uid}==0 || $files{$fil}{gid}==0 ||
        $files{$fil}{uid} > (2**32 -500) ||
        $files{$fil}{gid} > (2**32 -500)    ) {
        push @rootfiles, $fil;
        next;
    }
    if($files{$fil}{mode} & S_IFDIR) {
        if ( rmdir $fil ) {
            $favail++;
            $fsfree+=$files{$fil}{size};
            debug "rmdir '$fil'";
        }
    }
    else {
        if ( unlink $fil ) {
            $favail++;
            $fsfree+=$files{$fil}{size};
            debug "unlink '$fil'";
        } else {
            print STDERR "unlink '$fil': $!\n" unless $!{ENOENT};
        }
    }
}

if( @rootfiles ) {
    print "Root owns files marked for removal on ".hostname.":$filesystem\n";
    print join "\t\n", @rootfiles;
    print "\n";
}

exit 0;

sub wanted {
    my ($mode, $uid, $gid, $atime, $blocks);

    ($mode, $uid, $gid, $atime, $blocks) = (lstat($_))[2,4,5,8,12];
    return unless defined $uid;
    return if $name eq $filesystem;
    return if $name =~ m|$filesystem/$exclude|;

    return unless (!$opts{'d'} && -d _) || -f _ || -l _;

    $files{$name}{mode}=$mode;
    $files{$name}{uid}=$uid;
    $files{$name}{gid}=$gid;
    $files{$name}{atime}=$atime;
    $files{$name}{size}=$blocks * 512;
    $totsize += $files{$name}{size};
}

sub debug
{
    if( !defined($opts{'D'})) {
        return;
    }

    my (@args) = @_;

    print @args, "\n";
}

sub printsize($)
{
    my $size = shift;

    if($size > 10*1024*1024*1024*1024) {
        $size = int($size/(1024*1024*1024*1024));
        return "$size TiB";
    }
    if($size > 10*1024*1024*1024) {
        $size = int($size/(1024*1024*1024));
        return "$size GiB";
    }
    if($size > 10*1024*1024) {
        $size = int($size/(1024*1024));
        return "$size MiB";
    }
    if($size > 10*1024) {
        $size = int($size/1024);
        return "$size kiB";
    }

    return $size;
}

sub fsinfo
{
    my $filesystem = shift;
    my ($fssize,$fsfree,$isize,$ifree);

    # Test Filesys::Statvfs method
    eval {
        require Filesys::Statvfs;

        debug "Using Filesys:Statvfs";

        my($bsize, $frsize, $blocks, $bfree, $bavail, $files, $ffree, $favail,
        $fsid, $basetype, $flag, $namemax, $fstr) = Filesys::Statvfs::statvfs($filesystem);

        if(!defined($bsize)) {
            print STDERR "Unable to statvfs $filesystem: $!\n";
            exit 1;
        }

        $fssize = $frsize * $blocks;
        $fsfree = $frsize * $bavail;

        $isize = $favail;
        $ifree = $files;
    };

    if ($@) {
        debug "Using df -k / df -i";
        #
        # Not the best implementation, but it works =)
        # TODO: fix to work better with diffrent versions of df.

        my @dfk=`df -k $filesystem 2>/dev/null`;
        if ($? != 0) {
            print STDERR "Failed running: df -k $filesystem\n";

        # The first column may be printed on a separate line.
        # The relevant numbers are always on the last line.
        } elsif ($dfk[-1] =~ /\s+(\d+)\s+\d+\s+(\d+)\s+\d+%\s+\//) {
    	    $fssize = $1*1024;
    	    $fsfree = $2*1024;
        } else {
            print STDERR "Failed interpreting output of: df -k $filesystem\n";
            exit 1;
        }

        my @dfi=`df -i $filesystem 2>/dev/null`;
        if ($? != 0) {
            print STDERR "Failed running: df -i $filesystem\n";

        # The first column may be printed on a separate line.
        # The relevant numbers are always on the last line.
        } elsif ($dfi[-1] =~ /\s+(\d+)\s+\d+\s+(\d+)\s+\d+%\s+\//) {
    	    $isize = $1;
    	    $ifree = $2;
        } else {
            print STDERR "Failed interpreting output of: df -i $filesystem\n";
            exit 1;
        }

    }

    return ( $fssize, $fsfree, $isize, $ifree );
}
